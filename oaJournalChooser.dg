<* How to choose an open access journal
 *
 * Run this file by running:
 * java -jar DataTagsLib  
 *
 * available tags: complete, preprint, preprintFeedback, journalKnown, 
 * licenses, licenseKnown
 *
 * Current open questions:
 *   - Should the preprints part be in a separate chart? Is it a 
 *     recommendation or could it be compulsory? 
 *   - Contact Think.Check.Submit about using their check?
 *   - Does FORCE11 have a recommendation for licensing (CC BY 
 *     preferred? Ask authors to think of a good reason not to use CC BY?)
 *   - Still struggling with adding comment boxes to terminate: reject
 *     doesn't look right!
 *>
  

<-- let's ask about preprints
[>qPreprint< ask: {text: Have you submitted a preprint?}
{terms:
	 {preprints: a working paper or draft version posted online before peer review.}
}
{answers: 

<-- case where a preprint exists
	{yes: [set: preprint=yes]
		[ask: {text: Have you incorporated any feedback you received from the preprint?}
		{answers: 
			{yes: [set: preprintFeedback=yes]}
			{no: [set: preprintFeedback=no]
				 [call: qPreprint]}
		}]
	} <-- end of 'yes' (preprint submitted)
	
	<-- where no preprint exists
	{no: [set: preprint=no]
		 [todo: go to a separate tree about posting preprints]
	} 
}]

[>qJournal< ask: {text: Do you know which journal you will submit to?}
{terms:
	{open access: in this context, 'gold open access', i.e. a journal that makes paper freely available to download, reuse and mine as data immediately after publication.}
	{DOAJ: the directory of open access journals, http://doaj.org }
}
{answers:

	<-- fully open access journal
	{yes, fully OA: [set: journalKnown=oa]}
	
	<-- hybrid journal
	{yes, has OA option: [set: journalKnown=hybrid]}
	
	<-- no open access option
	{yes, not OA: [set: journalKnown=notOpen]}
	
	<-- don't know yet: direct towards DOAJ
	{no: [set: journalKnown=no]
		[ask: {text: Consult the Directory of Open Access Journals https://doaj.org }
		{answers:
			<-- go back and ask if they have chosen a journal yet
			{ok: [call: qJournal]}
		}]
	}
}]

<-- Check the Think.Check.Submit Guidelines
[ask: {text: Does the journal match most of the Think Check Submit guidelines?}
{terms:
	{think.check.submit: guidelines at thinkchecksubmit.org/check for choosing a good journal}
}
{answers:
	{yes: [set: tcs=yes]}
	{no: [set:tcs=no]
		 [call: qJournal]
	}
}]

<-- let's see about licensing
[ask: {text: Does the journal provide a choice of licenses?}
{terms:
	{copyright: defines ownership of the document and who has the right to determine its use.}
	{license: defines how the document can be used, including who can distribute, read or reuse it.}
	{creative commons: a licensing organisation that produces licenses intended to be open, see https://creativecommons.org. }
}
{answers:

	<-- range of licensing choices
	{yes: [set: licenses=many]}
	
	<-- only one choice of license
	{no: [set: licenses=single]}	
}]
